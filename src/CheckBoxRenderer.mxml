<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
	horizontalAlign="center" verticalAlign="middle"
	implements="mx.controls.listClasses.IDropInListItemRenderer,mx.managers.IFocusManagerComponent">
	<mx:Script><![CDATA[
		import mx.controls.DataGrid;
		import mx.controls.dataGridClasses.DataGridListData;
		import mx.controls.listClasses.BaseListData;
		
		/**
		 * 選択値
		 * DataGridColumn は editorDataField="selected" にすること
		 */
		public var selectedValue:Boolean;
		
		/** リストデータ */
		private var _listData:DataGridListData;
		
		
		/**
		 * フォーカス
		 * 
		 * @param draw	
		 */
		public override final function drawFocus(draw:Boolean):void {
			checkbox.setFocus();
		}
		
		/**
		 * 値の Setter
		 */
		[Bindable]
		override public final function set data(i_value:Object):void {
			super.data = i_value;
			var obj:Object = data[_listData.dataField];
			var work:String = "";
			if (obj is Boolean || obj is XMLList) {
				work = obj.toString();
			} else {
				work = obj as String;
			}
			if (work != null) {
				if (work == "true") {
					checkbox.selected = true;
				} else {
					checkbox.selected = false;
				}
			}
			selectedValue = checkbox.selected;
			trace("selected " + selectedValue);
		}

		/**
		 * 値の Getter
		 */
		public override final function get data():Object {
			return super.data;
		}
		
		/**
		 * リストデータの Getter
		 */
		public final function get listData():BaseListData {
			return _listData;
		}
		
		/**
		 * リストデータの Setter
		 */
		public final function set listData(i_value:BaseListData):void {
			_listData = DataGridListData(i_value);
		}

		private final function onChange():void {
			var dataField:String = DataGridListData(_listData).dataField;
			//data[dataField] = checkbox.selected?"true":"false";
			data[dataField] = data[dataField] == "true"?"false":"true";
			selectedValue = data[dataField] == "true";
			trace("updateData " + selectedValue);
		}
	]]></mx:Script>
	<!-- チェックボックス -->
	<mx:CheckBox id="checkbox" change="onChange()" />
</mx:HBox>
